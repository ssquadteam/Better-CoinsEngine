plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.0.0'
}

group = 'su.nightexpress.coinsengine'
version = '2.5.5'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'nightexpress-releases'
        url = 'https://repo.nightexpressdev.com/releases'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'placeholderapi'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = 'rosewood-repo'
        url = 'https://repo.rosewooddev.io/repository/public/'
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'org.black_ixx:playerpoints:3.0.0'
    compileOnly 'com.github.ItzSave:DeluxeCoinflipAPI:1.0.4'
    compileOnly name: 'nightcore-2.7.18', version: "2.7.18"
    compileOnly name: 'VaultUnlockedAPI-2.15'
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand(project.properties)
    }
}

shadowJar {
    archiveClassifier = ''
}

build {
    dependsOn shadowJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'nightexpress'
            url = 'https://repo.nightexpressdev.com/releases'
        }
    }
}
